buildscript {
  // Buildscript is evaluated before everything else so we can't use getExtOrDefault
  def kotlin_version = rootProject.ext.has('kotlinVersion') ? rootProject.ext.get('kotlinVersion') : project.properties['KenkouSdk_kotlinVersion']

  repositories {
    google()
    jcenter()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:4.1.0'
    // noinspection DifferentKotlinGradleVersion
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath "com.squareup.wire:wire-gradle-plugin:3.4.0"
  }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

def getExtOrDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties['KenkouSdk_' + name]
}

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties['KenkouSdk_' + name]).toInteger()
}

android {
  compileSdkVersion getExtOrIntegerDefault('compileSdkVersion')
  buildToolsVersion getExtOrDefault('buildToolsVersion')
  defaultConfig {
    minSdkVersion 23
    targetSdkVersion getExtOrIntegerDefault('targetSdkVersion')
    versionCode 1
    versionName "1.0"

  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }
  lintOptions {
    disable 'GradleCompatible'
  }
  compileOptions {
    coreLibraryDesugaringEnabled true
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

repositories {
  mavenCentral()
  jcenter()
  google()

  def found = false
  def defaultDir = null
  def androidSourcesName = 'React Native sources'

  if (rootProject.ext.has('reactNativeAndroidRoot')) {
    defaultDir = rootProject.ext.get('reactNativeAndroidRoot')
  } else {
    defaultDir = new File(
            projectDir,
            '/../../../node_modules/react-native/android'
    )
  }

  if (defaultDir.exists()) {
    maven {
      url defaultDir.toString()
      name androidSourcesName
    }

    logger.info(":${project.name}:reactNativeAndroidRoot ${defaultDir.canonicalPath}")
    found = true
  } else {
    def parentDir = rootProject.projectDir

    1.upto(5, {
      if (found) return true
      parentDir = parentDir.parentFile

      def androidSourcesDir = new File(
              parentDir,
              'node_modules/react-native'
      )

      def androidPrebuiltBinaryDir = new File(
              parentDir,
              'node_modules/react-native/android'
      )

      if (androidPrebuiltBinaryDir.exists()) {
        maven {
          url androidPrebuiltBinaryDir.toString()
          name androidSourcesName
        }

        logger.info(":${project.name}:reactNativeAndroidRoot ${androidPrebuiltBinaryDir.canonicalPath}")
        found = true
      } else if (androidSourcesDir.exists()) {
        maven {
          url androidSourcesDir.toString()
          name androidSourcesName
        }

        logger.info(":${project.name}:reactNativeAndroidRoot ${androidSourcesDir.canonicalPath}")
        found = true
      }
    })
  }

//  if (!found) {
//    throw new GradleException(
//            "${project.name}: unable to locate React Native android sources. " +
//                    "Ensure you have you installed React Native as a dependency in your project and try again."
//    )
//  }
}

def kotlin_version = getExtOrDefault('kotlinVersion')

dependencies {
  // noinspection GradleDynamicVersion
  api 'com.facebook.react:react-native:+'
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

  implementation files('libs/measurement-sdk-release-1.14.aar')

  implementation 'androidx.fragment:fragment-ktx:1.2.5'
  implementation 'androidx.appcompat:appcompat:1.2.0'
  implementation 'com.google.android.material:material:1.2.1'
  implementation 'androidx.constraintlayout:constraintlayout:2.0.1'

  implementation "androidx.camera:camera-camera2:1.0.0-beta10"
  implementation "androidx.camera:camera-lifecycle:1.0.0-beta10"
  implementation "androidx.camera:camera-view:1.0.0-alpha17"

  implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.2.0"
  implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"
  implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.2.0"

  implementation "androidx.room:room-runtime:2.2.5"
  implementation "androidx.room:room-ktx:2.2.5"

  implementation "androidx.work:work-runtime-ktx:2.4.0"

  implementation 'com.squareup.okhttp3:okhttp:4.2.2'
  implementation 'com.squareup.okhttp3:logging-interceptor:4.2.2'
  implementation "com.squareup.wire:wire-runtime:3.4.0"
  implementation "com.squareup.wire:wire-grpc-client:3.4.0"

  coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.0.10'
  implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
  implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2'
}
